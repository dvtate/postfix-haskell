# Import standard library (ie - math operators)
"./stdlib/prelude.phs" include use

# Above this is standard library

# Number type
I32 I64 | F32 F64 | | $Num =

# Parametric type
{ $A =
    { A A A } pack
} class $Vector3 =

# Generic Accessors
{ type Any Vector3 == } { unpack pop pop } $.x fun
{ type Any Vector3 == } { unpack pop swap pop } $.y fun
{ type Any Vector3 == } { unpack $z = pop pop z } $.z fun

# Overload * for dot-product
{ $b = $a =
    a type Num Vector3 ==
    b type Num Vector3 == &&
} {
    $b = $a =
    a .x  b .x *
    a .y  b .y * +
    a .z  b .z * +
} $* global fun

# Overload * for scalar multiplication
{ $v = $k =
    k type Num ==
    v type Num Vector3 == &&
} {
    $v = $k =
    {
        v .x k *
        v .y k *
        v .z k *
    } pack v type make
} $* global fun

# Project a onto b
{ $b = $a =
    a type Num Vector3 ==
    b type Num Vector3 == &&
} {
    $b = $a =
    a b *
    b b * /     # ||b|| ^ 2 = b dot b
    b *
} $proj fun

# Two perpendicular vectors
{ 1 1 0 } pack Num Vector3 make $v1 =
{ -1 1 0 } pack Num Vector3 make $v2 =

v1 v2 * :data # 0

# Project a onto b
{ 5.0 6.0 7.0 } pack Num Vector3 make $a =
{ 1.0 2.0 1.0 } pack Num Vector3 make $b =

a b proj :data # [ 4, 8, 4 ]

# Export

{ F64 } {
    $v =
    { v 1.0 v } pack Num Vector3 make $c =
    a b proj c *
} $test export

# (module
# (func $test (param $0 f64) (result f64)
#        (f64.add (f64.add (f64.mul (f64.const 4) (local.get 0)) (f64.const 8)) (f64.mul (f64.const 4) (local.get 0)))))