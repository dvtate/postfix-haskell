"./stdlib/prelude.phs" require use
"./stdlib/list.phs" require $list =

# Import Math.random from javascript
Unit (F64) Arrow ( "js" "Math.random" ) import $random =

# Type for lists of doubles
F64 list.List $DoubleList =

# Append n randomly generated nodes to given list
((I32 DoubleList) (DoubleList) rec:
    ( $ll $n ) =
    (: 1 ) (: ll ) $branch fun
    (: n 0 > ) (:
        ( random ll ) DoubleList.Node make
        n 1 -
        mk_list
    ) $branch fun
    branch
) $mk_list =

(I32) (:
    # Input: number of nodes to use in list
    $n =

    # A list with n randomly generated elements
    () DoubleList.Nil make n mk_list $list =

    # Give new list containing the triples of given list
    (: (: 3.0 * ) DoubleList.map ) $triple =

    # Calculate the sum of given list
    (: 0.0 (: + ) DoubleList.foldr ) $sum =

    # Return the sum of the tripled elements of generated list
    list triple sum
) "test" export
