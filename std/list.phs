"./lang.phs" require $prelude =

# Macro to construct a type for Linked Lists containing elements of given type
(:  $ElemType =

    # Using namespace prelude within this macro
    prelude use

    # Linked List with members of given type
    ( $ListType rec:
        ( ) type class $Nil =
        ( ElemType ListType ) class $Node =
    ) enum $ListType =

    # Member type of the list
    ElemType $ListType.ElemType =

    # Shortcut for empty list
    () ListType.Nil make $ListType.empty =

    # Append item to list head
    ((ElemType ListType):
        ( $m $l ) =
        ( m l ) ListType.Node make
    ) $cons =

    # Length of linked list
    (:  (rec:
            $ret =
            (   ListType.Nil (:
                    pop
                    ret
                )
                ListType.Node (:
                    unpack ( $v $next ) =
                    next  ret 1 + iter
                )
            ) match
        ) $iter =
        0 iter
    ) $ListType.length =

    # Get item at specific index in list
    "
    ((ListType I32):
        (rec: ( $ll $i ) =
            (
                ListType.Nil (:

                )

            ) match
            (: )
        ) $iter =
        iter
    ) $ListType.at =
    " pop


    # Apply function to each element of the list
    (( ListType  Syntax:Macro (ElemType) (Any) Arrow | ):
        $f =
        (rec:
            (   ListType.Nil (: ListType.Nil make )
                ListType.Node (:
                    unpack ( $v $n ) =
                    (   v f
                        n iter
                    ) ListType.Node make
                )
            ) match
        ) $iter =
        iter
    ) $ListType.map =

    # Reduce list into single value starting from the left
    # <list> <initial val> <accumulator> foldl
    (( ListType  Any  Syntax:Macro (Any ElemType) (Any) Arrow | ):
        $f =
        (rec:
            $z =
            (   ListType.Node (:
                    unpack ( $x $xs ) =
                    xs
                    z x f
                    iter
                )
                ListType.Nil (: pop z )
            ) match
        ) $iter =
        iter
    ) $ListType.foldl =

    # Like foldl but goes the other way around (note: not tail recursive)
    (( ListType  Any  Syntax:Macro (ElemType Any) (Any) Arrow | ):
        $f =
        (rec:
            $z =
            (   ListType.Node (:
                    unpack ( $x $xs ) =
                    x
                    xs z iter
                        f
                )
                ListType.Nil (: pop z )
            ) match
        ) $iter =
        iter
    ) $ListType.foldr =

    # Reverse the list
    (( ListType ):
        (rec: $ret =
            (   ListType.Nil (: pop ret )
                ListType.Node (:
                    unpack ( $x $xs ) =
                    xs
                    ( x ret ) ListType.Node make
                    iter
                )
            ) match
        ) $iter =
        () ListType.Nil make
    ) $ListType.reverse =

    (( ListType Syntax:Macro (ElemType) (I32) Arrow | ):
        $cond =
        (rec:
            $ret =
            (   ListType.Node (:
                    unpack ( $x $xs ) =
                    # Skip element if cond fails
                    (: 1 ) (: xs ret iter ) $branch fun
                    (: x cond ) (: ( x xs ret iter ) ListType.Node make )
                )
                ListType.Nil (: pop ret )
            ) match
        ) $iter =
        iter
    ) $ListType.filter =

    # TODO take drop concat ++ elem find some

    # Return the List type we created
    ListType
) $List =

# Honestly not sure if these should be in prelude ns anymore
(( Any List Syntax:Macro (Any) (Any) Arrow | ):
    ( $l $f ) =
    l type $LT =
    f type (LT.ElemType) (Any) Arrow Syntax:Macro | ==
)
(: ( $l $f ) = l type $ListType = l $f ~ ListType.map )
$prelude.map fun